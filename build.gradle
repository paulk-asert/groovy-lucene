plugins {
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'groovy'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.apache.groovy:groovy:5.0.0-alpha-11"
    implementation "org.apache.lucene:lucene-core:10.1.0"
    implementation "org.apache.lucene:lucene-queryparser:10.1.0"
    implementation "org.apache.lucene:lucene-highlighter:10.1.0"
    implementation "org.apache.lucene:lucene-analysis-common:10.1.0"
    implementation 'com.diogonunes:JColor:5.5.1'
}

sourceSets.main.allSource.files.each { file ->
    def name = "${file.name - '.groovy'}"
    if (name == 'Regex') return
    def subtask = tasks.register("run$name", JavaExec) {
        dependsOn compileGroovy
        group 'Application'
        description "Run $file.name as a JVM application/Groovy script"
        if (name.startsWith('Lucene')) {
            jvmArgs = ['--add-modules', 'jdk.incubator.vector']
        }
        classpath = sourceSets.main.runtimeClasspath
        mainClass = name
    }
}
